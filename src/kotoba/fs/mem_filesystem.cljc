(ns kotoba.fs.mem-filesystem
  "mem-filesystem -- addressed on its own.

  Split out of kotoba.lang.fs on 2026-09-09 (ADR-2609091200). The unit
  here is the DEFINITION, and this repo's deps.edn names exactly the
  definitions it reaches -- nothing else.
"
  (:require [kotoba.lang.text :as str]
            [kotoba.fs.ifilesystem :refer [IFilesystem delete exists? list read read-bytes write write-bytes]]
            [kotoba.fs.sep :refer [sep]]
            [kotoba.fs.split :refer [split]]
            [kotoba.fs.utf8-bytes :refer [utf8-bytes]]
            [kotoba.fs.utf8-text :refer [utf8-text]]))

(defn mem-filesystem
  "An atom-backed IFilesystem. Paths map to either a string (file content) or
  a nil placeholder (directory). Useful for tests and OSS-standalone apps; the
  host injects a real one in production."
  []
  (let [store (atom {})]
    (reify IFilesystem
      ;; A stored value is a string (written as text), a vector of unsigned
      ;; bytes (written as bytes), or nil (a directory placeholder). read and
      ;; read-bytes each convert whichever one is there, so the two faces
      ;; agree no matter which one wrote the file.
      (read       [_ path]
        (let [v (get @store path)]
          (if (vector? v) (utf8-text v) v)))
      (read-bytes [_ path]
        (let [v (get @store path)]
          (cond
            (vector? v) v
            (string? v) (utf8-bytes v))))
      (write   [_ path content] (swap! store assoc path (str content)) nil)
      (write-bytes [_ path bytes]
        (swap! store assoc path (mapv #(bit-and (int %) 0xff) bytes)) nil)
      (list    [_ path]
        (let [prefix (if (= path sep) sep (str path sep))
              ks (keys @store)]
          (->> ks
               (filter #(str/starts-with? % prefix))
               (map #(subs % (count prefix)))
               (map #(first (str/split % #"/")))
               distinct
               sort
               vec)))
      (exists? [_ path] (contains? @store path))
      (delete  [_ path] (swap! store dissoc path) nil))))
